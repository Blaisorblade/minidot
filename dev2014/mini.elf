%{ ------- language ----- }%

tpe: type.
dec: type.
topt: type.

top: tpe.
tbind: dec -> tpe.
tsel: tpe.

tnone: topt.
tsome: tpe -> topt.

rect: topt -> tpe -> dec.

%{ ------- environments ----- }%

% environments
tenv  : type.
tnil  : tenv.
tcons : dec -> tenv.

tlookup: tenv -> dec -> type.
tl  : tlookup (tcons D) D.

%{ ------- subtyping ------ }%

has-lower : dec -> tpe -> type.
has-lower/yes : has-lower (rect (tsome S) U) S.

has-upper : dec -> tpe -> type.
has-upper/yes : has-upper (rect _ U) U.

wf-tp : tenv -> tpe -> type.
wf-dc : tenv -> dec -> type.
sub-tp : tenv -> tpe -> tpe -> type.
sub-dc : tenv -> dec -> dec -> type.

sub-tp/top : sub-tp G T top
          <- wf-tp G top % redundant, but helps reduce regularity
          <- wf-tp G T.
sub-tp/tsel1 : sub-tp G tsel T
            <- tlookup G D
            <- wf-dc G D
            <- wf-tp G tsel % redundant, but helps reduce regularity
            <- has-upper D U
            <- sub-tp G U T
            .
sub-tp/tsel2 : sub-tp G T tsel
            <- tlookup G D
            <- wf-dc G D
            <- wf-tp G tsel % redundant, but helps reduce regularity
            <- has-lower D S
            <- sub-tp G T S
            .
sub-tp/tbind : sub-tp G (tbind D1) (tbind D2)
            <- wf-tp G (tbind D1) % redundant but helps reduce regularity
            <- wf-tp G (tbind D2)
            <- sub-dc (tcons D1) D1 D2
            .

sub-dc/ss : sub-dc G (rect (tsome S1) U1) (rect (tsome S2) U2)
         <- sub-tp G S2 S1
         <- sub-tp G U1 U2
         <- sub-tp G S1 U1
         <- sub-tp G S2 U2
         % redundant, but helps reduce regularity
         <- wf-dc G (rect (tsome S1) U1)
         <- wf-dc G (rect (tsome S2) U2)
         .
sub-dc/sn : sub-dc G (rect (tsome S1) U1) (rect tnone U2)
         <- sub-tp G U1 U2
         <- sub-tp G S1 U1
         % redundant, but helps reduce regularity
         <- wf-dc G (rect (tsome S1) U1)
         <- wf-dc G (rect tnone U2)
         .
sub-dc/nn : sub-dc G (rect tnone U1) (rect tnone U2)
         <- sub-tp G U1 U2
         % redundant, but helps reduce regularity
         <- wf-dc G (rect tnone U1)
         <- wf-dc G (rect tnone U2)
         .

wf-tp/top : wf-tp G top.
wf-tp/tbind : wf-tp G (tbind D)
           <- wf-dc (tcons D) D.
wf-tp/tsel : wf-tp G tsel
          <- tlookup G D
          <- wf-dc G D.

wf-dc/s : wf-dc G (rect (tsome S) U)
       <- sub-tp G S U.
wf-dc/n : wf-dc G (rect tnone U)
       <- wf-tp G U.

%%% PROOFS %%%

%{ ------- uniqueness ------ }%

same: tpe -> tpe -> type.
ident: same T T.

samed: dec -> dec -> type.
identd: samed T T.

sameopt : topt -> topt -> type.
identopt: sameopt T T.

sametenv: tenv -> tenv -> type.
identtenv: sametenv G G.

false: type.

tlookup-eq : tlookup G D1 -> tlookup G D2 -> samed D1 D2 -> type.
%mode tlookup-eq +A +B -C.

- : tlookup-eq tl tl identd.

%worlds () (tlookup-eq _ _ _).
%total A (tlookup-eq A _ _).

eq-sub-tp-low : same T1 T1' -> sub-tp G T1 T2 -> sub-tp G T1' T2 -> type.
%mode eq-sub-tp-low +A +B -C.

- : eq-sub-tp-low ident BT BT.

%worlds () (eq-sub-tp-low _ _ _).
%total A (eq-sub-tp-low A _ _).
%reduces C <= B (eq-sub-tp-low _ B C).

eq-sub-tp-high : same T2 T2' -> sub-tp G T1 T2 -> sub-tp G T1 T2' -> type.
%mode eq-sub-tp-high +A +B -C.

- : eq-sub-tp-high ident BT BT.

%worlds () (eq-sub-tp-high _ _ _).
%total A (eq-sub-tp-high A _ _).
%reduces C <= B (eq-sub-tp-high _ B C).

eq-sub-dc-low : samed D1 D1' -> sub-dc G D1 D2 -> sub-dc G D1' D2 -> type.
%mode eq-sub-dc-low +A +B -C.

- : eq-sub-dc-low identd BD BD.

%worlds () (eq-sub-dc-low _ _ _).
%total A (eq-sub-dc-low A _ _).
%reduces C <= B (eq-sub-dc-low _ B C).

eq-sub-dc-high : samed D2 D2' -> sub-dc G D1 D2 -> sub-dc G D1 D2' -> type.
%mode eq-sub-dc-high +A +B -C.

- : eq-sub-dc-high identd BD BD.

%worlds () (eq-sub-dc-high _ _ _).
%total A (eq-sub-dc-high A _ _).
%reduces C <= B (eq-sub-dc-high _ B C).

upper-eq : samed D D' -> has-upper D U -> has-upper D' U' -> same U U' -> type.
%mode upper-eq +A +B +C -D.

- : upper-eq identd has-upper/yes has-upper/yes ident.

%worlds () (upper-eq _ _ _ _).
%total A (upper-eq A _ _ _).

%{ ------- regularity ------ }%

extract-wf : sub-tp G T1 T2 -> wf-tp G T1 -> wf-tp G T2 -> type.
%mode extract-wf +A -B -C.

- : extract-wf (sub-tp/top W WT) W WT.

- : extract-wf (sub-tp/tsel1 B H W1 D L) W1 W2
 <- extract-wf B _ W2.

- : extract-wf (sub-tp/tsel2 B H W2 D L) W1 W2
 <- extract-wf B W1 _.

- : extract-wf (sub-tp/tbind BD W2 W1) W1 W2.

%worlds () (extract-wf _ _ _).
%total (A) (extract-wf A _ _).
%reduces {W1 W2} < {B B} (extract-wf B W1 W2).

extract-wfd : sub-dc G T1 T2 -> wf-dc G T1 -> wf-dc G T2 -> type.
%mode extract-wfd +A -B -C.

- : extract-wfd (sub-dc/ss W2 W1 B2 B1 BU BS) W1 W2.

- : extract-wfd (sub-dc/sn W2 W1 B1 BU) W1 W2.

- : extract-wfd (sub-dc/nn W2 W1 BU) W1 W2.

%worlds () (extract-wfd _ _ _).
%total (A) (extract-wfd A _ _).
%reduces {W1 W2} < {B B} (extract-wfd B W1 W2).

%{ ------- transitivity & weakening ------ }%

sub-env : tenv -> tenv -> type.
sub-env/tnil  : sub-env (tcons D) tnil.
sub-env/tcons : sub-env (tcons D1) (tcons D2)
            <- sub-dc (tcons D1) D1 D2.
