nat : type.  %name nat N.

z : nat.
s : nat -> nat.

add : nat -> nat -> nat -> type. %name add A.
add/z: add z N N.
add/s: add (s N1) N2 (s N3) <- add N1 N2 N3.


% ---------- types ---------- %

tp : type.  %name tp T.

top     : tp.
tp0     : tp.
tp1     : tp.
tp2     : tp.
tp3     : tp.
tp4     : tp.
tp5     : tp.
arrow   : tp -> tp -> tp.


exp : type. %name exp E.

ref     : nat -> exp.                % x pre-type index (DeBruijn)
var     : nat -> exp.                % x
lam     : tp -> exp -> exp.          % { z:T => B }

app     : exp -> exp -> exp.


tenv: nat -> type. %name tenv G.
tnil: tenv z.
tcons: tp -> tenv N -> tenv (s N).


tsize: tenv N -> nat -> type.
tsize/z : tsize tnil z.
tsize/s : tsize (tcons T G) (s N) <- tsize G N.


tlookup : tenv N -> nat -> tp -> type.
tlookup/s : tlookup (tcons T DS) N T' <- tlookup DS N T'.
tlookup/h : tlookup (tcons T DS) N T <- tsize DS N.


tsize-get: {N} {G:tenv N} tsize G N -> type.
%mode tsize-get +N +G -S.
- : tsize-get _ tnil tsize/z.
- : tsize-get (s N) (tcons _ G) (tsize/s S) <- tsize-get N G S.
%worlds () (tsize-get _ _ _).
%total (A) (tsize-get _ A _).


% ---------- open and wf ---------- %

open: exp -> nat -> nat -> exp -> type.

op/var: open (var X) I O (var X) <- add (s _) X O.

op/hit: open (ref I) I O (var O). % rule out (s x) etc ...

op/mis: open (ref J) I O (ref J) <- add (s _) J I.

op/app: open (app F1 F2) I O (app T1 T2) 
    <- open F2 I O T2 
    <- open F1 I O T1 .

op/lam: open (lam F1 F2) I O (lam F1 T2)
    <- open F2 (s I) O T2.


%query 1 1 open (lam top (lam top (app (ref (s z)) (ref (s (s z)))))) 
                z (s (s (s z)))
                (lam top (lam top (app (ref (s z)) (var (s (s (s z))))))).



wft  : {N} tenv N -> exp -> tp -> type.

wft/sel1    : wft     _ G (var N) T
           <- tlookup G N T
           .

wft/lam    : wft     N G (lam T E) (arrow T T')
           <- open E z N EN
           <- wft (s N) (tcons T G) EN T'
           .

wft/app   : wft     N G (app L1 U1) T2
           <- wft    N G U1 T1
           <- wft    N G L1 (arrow T1 T2)
            .


%query 1 1 wft z tnil (lam T0 (lam T1 (ref z))) (arrow T0 (arrow T1 T1)).



% ---------- wft2: combined open + wf ---------- %


tlookup : tenv N -> nat -> nat -> tp -> type.
tlookup/s : tlookup (tcons T DS) (s N1) N2 T' <- tlookup DS N1 N2 T'.
tlookup/h : tlookup (tcons T DS) z N2 T <- tsize DS N2.


%query 1 1 tlookup (tcons (arrow top top) (tcons top tnil)) _ z top.
%query 1 1 tlookup (tcons (arrow top top) (tcons top tnil)) z _ (arrow top top).


wft2  : {I:nat} {N} tenv N -> exp -> exp -> tp -> type.

wft2/ref   : wft2     I N G (ref Y) (ref Y) T  % X >= I --> stay ref
           <- tlookup G Y X T
           <- add I _ X
           .

wft2/op    : wft2     I N G (ref Y) (var X) T   % X < I --> open
           <- tlookup G Y X T
           <- add (s _) X I
           .

wft2/var   : wft2     I N G (var X) (var X) T   % X < I --> var ok
           <- tlookup G Y X T
           <- add (s _) X I
           .


wft2/lam    : wft2     I N G (lam T E) (lam T EN) (arrow T T')
           <- wft2 I (s N) (tcons T G) E EN T'
           .

wft2/app   : wft2    I N G (app L1 U1) (app L1' U1') T2
           <- wft2   I N G U1 U1' T1
           <- wft2   I N G L1 L1' (arrow T1 T2)
            .


%query 1 1 wft2 z     z tnil (lam T0 (lam T1 (ref (s z)))) E T.

%query 1 1 wft2 (s z) z tnil (lam T0 (lam T1 (ref (s z)))) E T.


e10 = (lam T0 (lam T1 (ref (s z)))).
e11 = (lam T0 (lam T1 (var z))).
e12 = (lam T0 (lam T1 (var z))).

%query 1 1 wft2 ((z))     z tnil e10 e10 T.
%query 1 1 wft2 ((s z))   z tnil e10 e11 T.
%query 1 1 wft2 (s (s z)) z tnil e10 e12 T.


e20 = (lam T0 (lam T1 (ref z))).
e21 = (lam T0 (lam T1 (ref z))).
e22 = (lam T0 (lam T1 (var (s z)))).

%query 1 1 wft2 ((z))     z tnil e20 e20 T.
%query 1 1 wft2 ((s z))   z tnil e20 e21 T.
%query 1 1 wft2 (s (s z)) z tnil e20 e22 T.




splice: {N0} tenv N0 -> tp -> {N1} tenv N1 -> tenv (s N1) -> type.
splice/refl : splice N G Z N G (tcons Z G).
splice/ext  : splice N G0 Z (s N1) (tcons T G1) (tcons T G2) <- splice N G0 Z N1 G1 G2.


%{

prove a bunch of contradictions, mostly out of bounds indexing

}%

false: type.

add-false0:
    add N1 (s _) N1 ->
    false -> 
    type.
%mode add-false0 +G -F.
- : add-false0 (add/s S) F <- add-false0 S F.
%worlds () (add-false0 _ _).
%total (A) (add-false0 A _).

add-z:
      {N}
      {S:add N z N}
      type.
%mode add-z +G -B.
- : add-z z add/z.
- : add-z (s N) (add/s S) <- add-z N S.
%worlds () (add-z _ _).
%total (A) (add-z A _).

add-s:
      {S:add N1 N2 N3}
      {S:add N1 (s N2) (s N3)}
      type.
%mode add-s +G -B.
- : add-s add/z add/z.
- : add-s (add/s S) (add/s S') <- add-s S S'.
%worlds () (add-s _ _).
%total (A) (add-s A _).

add-commute:
      {S:add N1 N2 N3}
      {S:add N2 N1 N3}
      type.
%mode add-commute +G -B.
- : add-commute add/z A <- add-z _ A.
- : add-commute (add/s A) A'' <- add-commute A A' <- add-s A' A''.
%worlds () (add-commute _ _).
%total (A) (add-commute A _).


add-false:
  {A3:add N1 D3 N2}
  {A2:add (s N2) D2 N3}
  {A1:add N3 D1 N1}
  false ->
  type.
%mode add-false +A +B +C -F.
- : add-false (add/s A3) (add/s A2) (add/s A1) F <- add-false A3 A2 A1 F.
%worlds () (add-false _ _ _ _).
%total (A) (add-false A _ _ _).


add-false1:
  {A':add N2 D N1}
  {A:add (s N1) D' N2}
  false ->
  type.
%mode add-false1 +G +A -B.
- : add-false1 (add/s S) (add/s S') F <- add-false1 S S' F.
%worlds () (add-false1 _ _ _).
%total (A) (add-false1 A _ _).

add-false2:
  {A':add D N2 N1}
  {A:add D' (s N1) N2}
  false ->
  type.
%mode add-false2 +G +A -B.
- : add-false2 A1 A2 F 
 <- add-commute A1 A1' 
 <- add-commute A2 A2' 
 <- add-false1 A1' A2' F.
%worlds () (add-false2 _ _ _).
%total (A) (add-false2 A _ _).

add-comm-false:
  {A':add N2 D N1}
  {A:add D' (s N1) N2}
  false ->
  type.
%mode add-comm-false +G +A -B.
- : add-comm-false A A'' F <- add-commute A A' <- add-false2 A' A'' F.
%worlds () (add-comm-false _ _ _).
%total (A) (add-comm-false A _ _).



size-bound:
      {G2:tenv N1}
      {L:tsize G2 N2}
      {S:add N2 z N1}
      type.
%mode size-bound +G +L -A.

- : size-bound _ (tsize/s S) (add/s A) <- size-bound _ S A.
- : size-bound _ (tsize/z) (add/z).

%worlds () (size-bound _ _ _).
%total (A) (size-bound _ A _).


lookup-bound:
      {G2:tenv N1}
      {L:tlookup G2 N2 N3 T3}
      {S:add (s D) N3 N1}
      type.
%mode lookup-bound +G +L -F.

- : lookup-bound _ (tlookup/s S) (add/s A) 
        <- lookup-bound _ S A.
- : lookup-bound _ (tlookup/h S) (add/s A') 
        <- size-bound _ S A 
        <- add-commute A A'.

%worlds () (lookup-bound _ _ _).
%total (A) (lookup-bound _ A _).


lookup-false:
      {G2:tenv N1}
      {L:tlookup G2 N2 N3 T3}
      {S:add D N1 N3}
      false ->
      type.
%mode lookup-false +G +L +A -F.

- : lookup-false _ (tlookup/s L) A F 
        <- lookup-bound _ L A'
        <- add-false2 A' A F.

- : lookup-false _ (tlookup/h S) A F 
        <- size-bound _ S A' 
        <- add-commute A' A''
        <- add-false2 A'' A F.

%worlds () (lookup-false _ _ _ _).
%total (A) (lookup-false _ A _ _).


contra-lookup:
      {G2}{Y}{X}{T}
      false ->
      tlookup G2 Y X T ->
      type.
%mode contra-lookup +G +Y +X +T +F -L.
%worlds () (contra-lookup _ _ _ _ _ _).
%total (A) (contra-lookup A _ _ _ _ _).


size-from-splice:
      splice N G0 Z N2 G2 G2' ->
      tsize G2 N3 ->
      tsize G2' (s N3) ->
      type.
%mode size-from-splice +G +A -C.

-: size-from-splice splice/refl S (tsize/s S).
-: size-from-splice (splice/ext SP) (tsize/s S) (tsize/s S')
    <- size-from-splice SP S S'.

%worlds () (size-from-splice _ _ _).
%total (A) (size-from-splice A _ _).

xx1:
  {A:add D (s N3) N1}
  {A:add (s D) N3 N1}
  type.
%mode xx1 +SP -B.
- : xx1 add/z (add/s add/z).
- : xx1 (add/s S) (add/s S') <- xx1 S S'.

%worlds () (xx1 _ _).
%total (A) (xx1 A _).

xx1a:
  {A2:add (s N1) N2 N2}
  false ->
  type.
%mode xx1a +A -F.
- : xx1a A F <- add-commute A A' <- add-false0 A' F.
%worlds () (xx1a _ _).
%total (A) (xx1a A _).

xx1b:
  {A2:add (s N1) N2 N3}
  {A1:add z N2 N3}
  false ->
  type.
%mode xx1b +A +B -F.
- : xx1b A1 add/z F <- xx1a A1 F.
%worlds () (xx1b _ _ _).
%total (A) (xx1b A _ _).

xx2:
  {G1:tenv N1}
  {S:tsize G1 N2}
  {A:add N3 (s N2) N1}
  false ->
  type.
%mode xx2 +G +S +A -F.
- : xx2 G S A F
  <- size-bound G S A'
  <- add-commute A' A2
  <- xx1 A A1
  <- xx1b A1 A2 F.

%worlds () (xx2 _ _ _ _).
%total (A) (xx2 A _ _ _).


splice-size-false:
  {SP:splice N1 G1 T1 N2 G2 G3} 
  {S:tsize G2 N3}
  {A:add (s D) N3 N1}
  false ->
  type.
%mode splice-size-false +SP +L +A -F.

- : splice-size-false (splice/ext SP) (tsize/s S) A F 
      <- xx1 A A'
      <- splice-size-false SP S A' F.

- : splice-size-false splice/refl (tsize/s S) (add/s A) F 
      <- xx2 _ S A F.

%worlds () (splice-size-false _ _ _ _).
%total (A) (splice-size-false A _ _ _).


%% splice preserves ref lookup (N < X)
lookup-splice-ref: 
      splice N G0 Z N2 G2 G2' ->
      tlookup G2 Y X T ->
      add N _ X ->
      tlookup G2' Y (s X) T ->
      type.
%mode lookup-splice-ref +SP +L +A -L'.


- : lookup-splice-ref splice/refl L A L' % impossible: no lookup at N
        <- add-commute A A'
        <- lookup-false _ L A' F
        <- contra-lookup _ _ _ _ F L'.

- : lookup-splice-ref (splice/ext SP) (tlookup/h S) A (tlookup/h S')
        <- size-from-splice SP S S'. %% uniqueness of size

- : lookup-splice-ref (splice/ext SP) (tlookup/s L) S (tlookup/s L') 
        <- lookup-splice-ref SP L S L'.

%worlds () (lookup-splice-ref _ _ _ _).
%total (A) (lookup-splice-ref _ A _ _).

%% splice preserves var lookup (X < N)
lookup-splice-var: 
      splice N G0 Z N2 G2 G2' ->
      tlookup G2 Y X T ->
      add (s _) X N ->
      tlookup G2' (s Y) X T ->
      type.
%mode lookup-splice-var +SP +L +A -L'.

- : lookup-splice-var splice/refl L A (tlookup/s L).

- : lookup-splice-var (splice/ext SP) (tlookup/s L) A (tlookup/s L') 
        <- lookup-splice-var SP L A L'.

- : lookup-splice-var (splice/ext SP) (tlookup/h S) A L'
        <- splice-size-false SP S A F
        <- contra-lookup _ _ _ _ F L'.

%worlds () (lookup-splice-var _ _ _ _).
%total (A) (lookup-splice-var _ A _ _).



extend-splice-wf2:
      splice N G0 Z N2 G2 G2' ->
      {S2 :wft2 N N2 G2 E1 E1 U}
      {S2':wft2 (s N) (s N2) G2' E1 E1 U}
      type.

%mode extend-splice-wf2 +SP +W1 -W2.

- : extend-splice-wf2 SP (wft2/ref A L) (wft2/ref (add/s A) L')
      <- lookup-splice-ref SP L A L'.

- : extend-splice-wf2 SP (wft2/var A L) (wft2/var (add/s A) L')
      <- lookup-splice-var SP L A L'.

- : extend-splice-wf2 SP (wft2/lam S2) (wft2/lam S2')
      <- extend-splice-wf2 (splice/ext SP) S2 S2'
      .

- : extend-splice-wf2 SP (wft2/app S1 S2) (wft2/app S1' S2')
      <- extend-splice-wf2 SP S1 S1'
      <- extend-splice-wf2 SP S2 S2'
      .


%worlds () (extend-splice-wf2 _ _ _).
%total (A) (extend-splice-wf2 _ A _).


extend-wf2:      
      {S2 :wft2 N N G E1 E1 U}
      {T}
      {S2':wft2 (s N) (s N) (tcons T G) E1 E1 U}
      type.

%mode extend-wf2 +SP +W1 -W2.

- : extend-wf2 S1 Z S1' <- extend-splice-wf2 splice/refl S1 S1'.

%worlds () (extend-wf2 _ _ _).
%total (A) (extend-wf2 _ A _).


